#!/usr/bin/env perl

#------------------------------------------------------------------------------
# Z80 assembler
# Copyright (C) Paulo Custodio, 2011-2026
# License: The Artistic License 2.0, http://www.perlfoundation.org/artistic_license_2_0
#------------------------------------------------------------------------------

# Make keywords.def filters based on flag

use Modern::Perl;
use Data::Dump 'dump';
use FindBin;
use lib "$FindBin::Bin/cpu";
use Keywords;

@ARGV==1 or die "Usage $0 keywords.def\n";
my $keywords_def_file = shift;

my $keywords = Keywords->new;
$keywords->read_from_file($keywords_def_file);

# get list of flags
my %flags;
for my $keyword (@{ $keywords->list }) {
	my @flags = split /\s*\|\s*/, $keyword->{flags};
	$flags{$_} = 1 for @flags;
}

# dump one filter file per flag
for my $flag (sort keys %flags) {
	next if $flag eq '0';
	my $flag_text = lc($flag =~ s/^IS_//r);

	my $out_filename = $keywords_def_file =~ s/\.def/_$flag_text.inc/r;
	open(my $fh, ">", $out_filename) or die "open $out_filename: $!";
	say $fh "// $keywords_def_file filtered by $flag";
	say $fh "// generated by: $0 $keywords_def_file";
	say $fh "";
	
	for my $keyword (@{ $keywords->list }) {
		my $exists = $keyword->{flags} =~ /$flag/ ? 1 : 0;
		my $id = $exists ? $keyword->{id} =~ s/^Keyword:://r : "_";
		say $fh "X($exists, $id)";
	}
}
